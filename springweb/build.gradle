plugins {
	id 'org.springframework.boot' version '2.2.1.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
}



group = 'com.springweb'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}
/**
Unpack dependencies to the docker folder
*/
task unpack(type: Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("docker/dependency")
}
/**
Copy jar file to the docker folder
*/
task copyJar(type: Copy) {
	dependsOn  build
	from        "build/libs/${jar.baseName}-${jar.version}.jar"
	into        "docker"

	rename { String fileName ->
		fileName.replace("-${jar.version}", "")
	}
}
/**
Prepares docker folder
*/
task docker(){
	dependsOn copyJar
	dependsOn unpack
}
test {
	useJUnitPlatform()
}
